-- Tokens

token UIdent (upper (letter | digit | '_')*) ;

entrypoints Program ;

-- Top level.

Prog. Program ::= [TopLvlDecl] ;

TopLvlFDecl. TopLvlDecl ::= FunDecl ;
TopLvlTHint. TopLvlDecl ::= TypeHint ;
TopLvlTDecl. TopLvlDecl ::= TypeDecl ;
terminator TopLvlDecl ";" ;

-- Types.

THint. TypeHint ::= Ident "::" TypeExpr ;

TVar.   TypeExpr2  ::= Ident ;
TCtor.  TypeExpr2  ::= UIdent ;
TPur.   TypeExpr2  ::= TypePurity ;
TUnit.  TypeExpr2  ::= "()" ;
TTup.   TypeExpr2  ::= "(" TupleType ")" ;
TAdHoc. TypeExpr2  ::= "{" [FieldTypeExpr] "}" ;
TApp.   TypeExpr1  ::= UIdent [TypeExpr2] ;
TFun.   TypeExpr  ::= TypeExpr1 "->" TypeExpr ;

separator nonempty TypeExpr2 "";
coercions TypeExpr 2 ;

TTupList. TupleType ::= TypeExpr "," TupleType ;
TTupTail. TupleType ::= TypeExpr "," TypeExpr ;

TImpure. TypePurity ::= "impure" ;
TSideE.  TypePurity ::= "sideeffect" ;

TFld. FieldTypeExpr ::= TypeHint ;
separator FieldTypeExpr "," ;

-- Functions

FDecl. FunDecl ::= Ident [FunParam] "=" FunBody ;

FParam. FunParam ::= Ident ;
separator FunParam "" ;

LamParam. LambdaParam ::= Pattern ;
separator LambdaParam "" ;

FExprBody. FunBody ::= Expression ;
FStmtBody. FunBody ::= Statement5 ;
-- Used for function bodies converted to iterators.
internal FVIterBody. FunBody ::= Statement5 ;
internal FRIterBody. FunBody ::= Statement5 ;

-- Literals

BTrue.  BoolLiteral ::= "true" ;
BFalse. BoolLiteral ::= "false" ;

UnitLit. Literal ::= "()" ;
IntLit.  Literal ::= Integer ;
CharLit. Literal ::= Char ;
StrLit.  Literal ::= String ;
BoolLit. Literal ::= BoolLiteral ;

-- Data access

MembAcc. MemberAccess ::= "." Ident ;

separator nonempty MemberAccess "" ;

-- Expressions

ThisExpr.  Expression13 ::= "this" ;
TupExpr.   Expression13 ::= "(" TupleExpression ")" ;
LitExpr.   Expression13 ::= Literal ;
ObjExpr.   Expression13 ::= Ident ;

TMembExpr. Expression12 ::= UIdent [MemberAccess] ;
MembExpr.  Expression12 ::= Expression13 [MemberAccess] ;
DataExpr.  Expression12 ::= "this.data" [MemberAccess] ;

AdHocExpr. Expression11 ::= "val" "{" [AdHocFieldDecl] "}" ;
VCtorExpr. Expression11 ::= "val" UIdent "{" [FieldAss] "}" ;

MatchExpr. Expression10 ::= "match" Expression11 "{" [MatchExpressionClause] "}" ;

AppExpr.   Expression9 ::= Expression9 Expression11 ;

CompExpr.  Expression8 ::= Expression8 "@" Expression9 ;

PowExpr.   Expression7 ::= Expression7 "^" Expression8 ;

MulExpr.   Expression6 ::= Expression6 "*" Expression7 ;
DivExpr.   Expression6 ::= Expression6 "/" Expression7 ;
ModExpr.   Expression6 ::= Expression6 "mod" Expression7 ;

AddExpr.   Expression5 ::= Expression5 "+" Expression6 ;
SubExpr.   Expression5 ::= Expression5 "-" Expression6 ;

NotExpr.   Expression4 ::= "not" Expression9 ;
NegExpr.   Expression4 ::= "-"   Expression9 ;

EqExpr.    Expression3 ::= Expression3 "==" Expression4 ;
NEqExpr.   Expression3 ::= Expression3 "!=" Expression4 ;
LtExpr.    Expression3 ::= Expression3 "<" Expression4 ;
GtExpr.    Expression3 ::= Expression3 ">" Expression4 ;
LEqExpr.   Expression3 ::= Expression3 "<=" Expression4 ;
GEqExpr.   Expression3 ::= Expression3 ">=" Expression4 ;

AndExpr.   Expression2 ::= Expression2 "and" Expression3 ;
OrExpr.    Expression2 ::= Expression2 "or" Expression3 ;

SeqExpr.   Expression1 ::= Expression2 "|" Expression1 ;

LamExpr.   Expression ::= "\\" [LambdaParam] "=>" FunBody ;

coercions Expression 13;

-- Tuple expressions.

TupExprList. TupleExpression ::= Expression11 "," TupleExpression ;
TupExprTail. TupleExpression ::= Expression11 "," Expression11 ;

-- Match expressions.

MatchExprClause. MatchExpressionClause ::= Pattern "=>" Expression ;
separator nonempty MatchExpressionClause "," ;

-- Value construction expressions.

DataAss. FieldAss ::= Ident "=" Expression ;
separator FieldAss "," ;

-- Statements.

EmptyStmt.       Statement5 ::= "{ }" ;
StmtBlock.       Statement5 ::= "{" [Statement] "}" ;
StmtBlockWDecls. Statement5 ::= "{" [Statement] "where" [LocalFunDecl] "}" ;

RetStmt.         Statement4 ::= "return" ";" ;
RetExprStmt.     Statement4 ::= "return" Expression ";" ;
CntStmt.         Statement4 ::= "continue" ";" ;
BrkStmt.         Statement4 ::= "break" ";" ;
YieldStmt.       Statement4 ::= "yield" Expression ";" ;
YieldRetStmt.    Statement4 ::= "yield" "return" ";" ;
_.               Statement4 ::= Statement5 ;

MatchStmt.       Statement3 ::= "match" Expression "{" [MatchStatementClause] "}" ;
WhileStmt.       Statement3 ::= "while" Expression Statement5 ;
ForInStmt.       Statement3 ::= "for" Pattern "in" Expression Statement5 ;
CondStmt.        Statement3 ::= ConditionalStatement ;
_.               Statement3 ::= Statement4 ;
-- Used to convert to `iterate` calls.
internal ForInVStmt. Statement3 ::= "for" Pattern "in" Expression Statement5 ;
internal ForInRStmt. Statement3 ::= "for" Pattern "in" Expression Statement5 ;

DconStmt.        Statement2 ::= Pattern "=" Expression ";" ;
DeclStmt.        Statement2 ::= LocalObjDecl1 ";" ;
_.               Statement2 ::= Statement3 ;

AssStmt.         Statement1 ::= Ident ":=" Expression ";" ;
AddStmt.         Statement1 ::= Ident "+=" Expression ";" ;
SubStmt.         Statement1 ::= Ident "-=" Expression ";" ;
MulStmt.         Statement1 ::= Ident "*=" Expression ";" ;
DivStmt.         Statement1 ::= Ident "/=" Expression ";" ;
PowStmt.         Statement1 ::= Ident "^=" Expression ";" ;
CompStmt.        Statement1 ::= Ident "@=" Expression ";" ;
DataAddStmt.     Statement1 ::= "this.data" "." Ident "+=" Expression ";" ;
DataSubStmt.     Statement1 ::= "this.data" "." Ident "-=" Expression ";" ;
DataMulStmt.     Statement1 ::= "this.data" "." Ident "*=" Expression ";" ;
DataDivStmt.     Statement1 ::= "this.data" "." Ident "/=" Expression ";" ;
DataAssStmt.     Statement1 ::= "this.data" "." Ident ":=" Expression ";" ;
DataPowStmt.     Statement1 ::= "this.data" "." Ident "^=" Expression ";" ;
DataCompStmt.    Statement1 ::= "this.data" "." Ident "@=" Expression ";" ;
_.               Statement1 ::= Statement2 ;

EvalStmt.        Statement ::= "eval" Expression9 ";" ;
_.               Statement ::= Statement1 ;
separator nonempty Statement "" ;

-- Match statements.

MatchStmtClause. MatchStatementClause ::= Pattern "=>" Statement4 ;
separator nonempty MatchStatementClause "" ;

-- Conditional statements.

IfElifStmts. ConditionalStatement ::= IfStatement [ElseIfStatement] ;
IfElifElseStmts. ConditionalStatement ::= IfStatement [ElseIfStatement] ElseStatement ;

IfStmt. IfStatement ::= "if" Expression Statement5 ;
ElifStmt. ElseIfStatement ::= "else if" Expression Statement5 ;
ElseStmt. ElseStatement ::= "else" Statement5 ;

separator ElseIfStatement "" ;

-- Patterns.

PatLit.  Pattern2 ::= Literal ;
_.       Pattern1 ::= Pattern2 ;
PatDecl. Pattern1 ::= LocalObjDecl ;
PatData. Pattern1 ::= "val" "{" [FieldPattern] "}" ;
PatTup.  Pattern1 ::= "(" TuplePattern ")" ;
PatDisc. Pattern1 ::= "_" ;
_.       Pattern ::= Pattern1 ;
PatCtor. Pattern ::= UIdent "{" [FieldPattern] "}" ;

-- Tuple patterns.

PatTupList. TuplePattern ::= Pattern "," TuplePattern ;
PatTupTail. TuplePattern ::= Pattern "," Pattern ;

-- Data patterns.

PatFld. FieldPattern ::= Ident ":" Pattern ;
separator FieldPattern "," ;

-- Local declarations.

Decl.      Declaration ::= Ident ;
DeclWHint. Declaration ::= "(" TypeHint ")" ;

AdHocFld. AdHocFieldDecl ::= Declaration "=" Expression ;
terminator AdHocFieldDecl ";" ;

LocTHint. LocalFunDecl ::= TypeHint ;
LocFDecl. LocalFunDecl ::= FunDecl ;
LocVarDecl. LocalObjDecl1 ::= "var" Declaration ;
_.          LocalObjDecl ::= LocalObjDecl1 ;
LocValDecl. LocalObjDecl ::= Declaration ;

terminator nonempty LocalFunDecl ";" ;

-- Type declarations.

TSig. TypeSignature ::= UIdent [TypeParameter] ;

ValTDecl.  TypeDecl ::= "value" TypeSignature "=" "{" TypeBody "}" ;
ValTUDecl. TypeDecl ::= "value" TypeSignature "=" "{" [TypeVariantDecl] "}" ;
RefTDecl.  TypeDecl ::= "ref" TypeSignature "=" "{" TypeBody "}" ;

TParam. TypeParameter ::= Ident ;
separator TypeParameter "" ;

TVarDecl. TypeVariantDecl ::= "variant" UIdent "=" "{" TypeBody "}";
terminator nonempty TypeVariantDecl ";" ;

DataTBody. TypeBody ::= "data" "=" "{" [FieldDecl] "}" [MemberDecl] ;
TBody.     TypeBody ::= [MemberDecl] ;

TFldDecl. FieldDecl ::= TypeHint ;
terminator nonempty FieldDecl ";" ;

TMemTHint. MemberDecl ::= TypeHint ;
TMemFDecl. MemberDecl ::= FunDecl ;
terminator MemberDecl ";" ;

comment "//" ;
comment "/*" "*/" ;
